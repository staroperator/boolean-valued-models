import BooleanValuedModels.BooleanAlgebra.Lemmas
import Mathlib.Tactic.FunProp

universe u v

@[pp_with_univ]
inductive BVSet (B : Type u)
| mk (Î¹ : Type v) (dom : Î¹ â†’ BVSet B) (val : Î¹ â†’ B)
namespace BVSet

variable {B : Type u}

def Index : BVSet B â†’ Type v
| mk Î¹ _ _ => Î¹

@[simp] theorem Index_mk {Î¹ : Type v} {dom : Î¹ â†’ BVSet B} {val} : (mk Î¹ dom val).Index = Î¹ := rfl

instance : CoeSort (BVSet B) (Type v) := âŸ¨IndexâŸ©

def dom : (x : BVSet B) â†’ x.Index â†’ BVSet B
| mk _ dom _ => dom

@[simp] theorem dom_mk {Î¹ : Type v} {dom : Î¹ â†’ BVSet B} {val} : (mk Î¹ dom val).dom = dom := rfl

instance {x : BVSet B} : CoeOut x.Index (BVSet B) := âŸ¨x.domâŸ©

def val : (x : BVSet B) â†’ x.Index â†’ B
| mk _ _ val => val

@[simp] theorem val_mk {Î¹ : Type v} {dom : Î¹ â†’ BVSet B} {val} : (mk Î¹ dom val).val = val := rfl

instance : CoeFun (BVSet B) (Î» x => x â†’ B) := âŸ¨valâŸ©

variable [CompleteBooleanAlgebra B]

def eq : BVSet.{u, v} B â†’ BVSet.{u, v} B â†’ B
| âŸ¨u, udom, uvalâŸ©, âŸ¨v, vdom, vvalâŸ© =>
  (â¨… x : u, uval x â‡¨ â¨† y : v, vval y âŠ“ (udom x).eq (vdom y)) âŠ“
    â¨… y : v, vval y â‡¨ â¨† x : u, uval x âŠ“ (udom x).eq (vdom y)

infix:70 " =á´® " => eq

def mem : BVSet.{u, v} B â†’ BVSet.{u, v} B â†’ B
| u, v => â¨† x : v, v x âŠ“ u.eq x

infix:70 " âˆˆá´® " => mem

def subset : BVSet.{u, v} B â†’ BVSet.{u, v} B â†’ B
| u, v => â¨… x : u, u x â‡¨ (x : BVSet B).mem v

infix:70 " âŠ†á´® " => subset

theorem eq_refl (u : BVSet B) : u =á´® u = âŠ¤ := by
  rcases u with âŸ¨u, udom, uvalâŸ©
  rw [BVSet.eq]
  simp only [inf_eq_top_iff, iInf_eq_top, himp_eq_top_iff]
  constructor <;> intro x <;> apply le_iSup_of_le x <;> simp [eq_refl]

theorem eq_symm (u v : BVSet B) : u =á´® v = v =á´® u := by
  rcases u with âŸ¨u, udom, uvalâŸ©
  rcases v with âŸ¨v, vdom, vvalâŸ©
  rw [BVSet.eq, BVSet.eq]
  conv_lhs => rw [inf_comm]
  congr! 7 <;> apply eq_symm

theorem mem_def {u v : BVSet B} : u âˆˆá´® v = â¨† x : v, v x âŠ“ u =á´® x := rfl

theorem subset_def {u v : BVSet B} : u âŠ†á´® v = â¨… x : u, u x â‡¨ x âˆˆá´® v := rfl

theorem eq_def {u v : BVSet B} : u =á´® v = u âŠ†á´® v âŠ“ v âŠ†á´® u := by
  rcases u with âŸ¨u, udom, uvalâŸ©
  rcases v with âŸ¨v, vdom, vvalâŸ©
  rw [BVSet.eq, BVSet.subset, BVSet.subset]
  simp only [val_mk, dom_mk, mem_def]
  conv_rhs => enter [2, 1, x, 2, 1, y]; rw [eq_symm]
  rfl

lemma eq_inf_val_le_mem {u v : BVSet B} {x : u} : u =á´® v âŠ“ u x â‰¤ x âˆˆá´® v := by
  rw [eq_def, subset_def]
  apply (inf_le_inf_right _ (inf_le_of_left_le (iInf_le _ x))).trans
  simp

lemma eq_inf_val_le_mem' {u v : BVSet B} {x : v} : u =á´® v âŠ“ v x â‰¤ x âˆˆá´® u := by
  rw [eq_symm]
  exact eq_inf_val_le_mem

theorem eq_trans (u v w : BVSet B) : u =á´® v âŠ“ v =á´® w â‰¤ u =á´® w := by
  rcases u with âŸ¨u, udom, uvalâŸ©
  rcases v with âŸ¨v, vdom, vvalâŸ©
  rcases w with âŸ¨w, wdom, wvalâŸ©
  conv_rhs => rw [eq_def]
  simp only [subset_def, le_inf_iff, le_iInf_iff, le_himp_iff, Index_mk, dom_mk, val_mk]
  constructor
  Â· intro x
    rw [inf_right_comm]
    apply (inf_le_inf_right _ eq_inf_val_le_mem).trans
    rw [mem_def, iSup_inf_eq]
    simp only [Index_mk, val_mk, dom_mk, iSup_le_iff]
    intro y
    rw [inf_right_comm, inf_comm (vval y)]
    apply (inf_le_inf_right _ eq_inf_val_le_mem).trans
    simp only [dom_mk, mem_def, Index_mk, val_mk]
    rw [iSup_inf_eq]
    refine iSup_mono fun z => ?_
    rw [inf_assoc, inf_comm (vdom y =á´® wdom z)]
    apply inf_le_inf_left
    apply eq_trans
  Â· intro z
    rw [inf_assoc]
    apply (inf_le_inf_left _ eq_inf_val_le_mem').trans
    rw [mem_def, inf_iSup_eq]
    simp only [Index_mk, val_mk, dom_mk, iSup_le_iff]
    intro y
    rw [â† inf_assoc]
    apply (inf_le_inf_right _ eq_inf_val_le_mem').trans
    simp only [dom_mk, mem_def, Index_mk, val_mk]
    rw [iSup_inf_eq]
    refine iSup_mono fun x => ?_
    rw [inf_assoc, inf_comm (vdom y =á´® udom x)]
    apply inf_le_inf_left
    apply eq_trans

variable {u v w : BVSet B}

theorem eq_trans' (u v w : BVSet B) : v =á´® w âŠ“ u =á´® v â‰¤ u =á´® w := by
  rw [inf_comm]
  apply eq_trans

theorem val_le_mem {x : u} : u x â‰¤ x âˆˆá´® u := by
  rw [mem_def]
  apply le_iSup_of_le x
  simp [eq_refl]

theorem mem_congr_left (u v w : BVSet B) : u =á´® v âŠ“ u âˆˆá´® w â‰¤ v âˆˆá´® w := by
  rw [mem_def, inf_iSup_eq, mem_def]
  refine iSup_mono fun z => ?_
  rw [inf_left_comm, eq_symm u]
  exact inf_le_inf_left _ <| eq_trans _ _ _

theorem mem_congr_left' (u v w : BVSet B) : u =á´® v âŠ“ v âˆˆá´® w â‰¤ u âˆˆá´® w := by
  rw [eq_symm]
  apply mem_congr_left

theorem mem_congr_right (u v w : BVSet B) : v =á´® w âŠ“ u âˆˆá´® v â‰¤ u âˆˆá´® w := by
  rw [mem_def, inf_iSup_eq, iSup_le_iff]
  intro y
  rw [â† inf_assoc]
  apply (inf_le_inf_right _ eq_inf_val_le_mem).trans
  rw [inf_comm, eq_symm]
  apply mem_congr_left

theorem mem_congr_right' (u v w : BVSet B) : v =á´® w âŠ“ u âˆˆá´® w â‰¤ u âˆˆá´® v := by
  rw [eq_symm]
  apply mem_congr_right



@[fun_prop] def IsExtentionalFun (f : BVSet.{u, v} B â†’ BVSet.{u, v} B) :=
  âˆ€ x y, x =á´® y â‰¤ f x =á´® f y

@[fun_prop] theorem IsExtentionalFun.id : IsExtentionalFun Î» x : BVSet B => x := Î» x y => by simp

@[fun_prop] theorem IsExtentionalFun.const {a : BVSet B} : IsExtentionalFun Î» _ => a := Î» x y => by simp [eq_refl]

@[fun_prop] theorem IsExtentionalFun.comp {f g : BVSet B â†’ BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) : IsExtentionalFun (f âˆ˜ g) :=
  Î» x y => (hg x y).trans (hf _ _)

@[fun_prop] def IsExtentional (f : BVSet B â†’ B) :=
  âˆ€ x y, x =á´® y âŠ“ f x â‰¤ f y

theorem IsExtentional.eq_inf_le (f) (hf : IsExtentional f) (u v : BVSet B) : u =á´® v âŠ“ f u â‰¤ f v := hf u v

theorem IsExtentional.eq_inf_le' (f) (hf : IsExtentional f) (u v : BVSet B) : v =á´® u âŠ“ f u â‰¤ f v := by
  rw [eq_symm]
  apply hf

@[fun_prop] theorem IsExtentional.const {a : B} : IsExtentional Î» _ => a := Î» x y => by simp

@[fun_prop] theorem IsExtentional.comp {f : BVSet B â†’ B} {g : BVSet B â†’ BVSet B}
    (hf : IsExtentional f) (hg : IsExtentionalFun g) : IsExtentional (f âˆ˜ g) :=
  Î» x y => by grw [hg x y]; apply hf

@[fun_prop] theorem IsExtentional.eq {f g : BVSet B â†’ BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) : IsExtentional Î» x => f x =á´® g x := Î» x y => by
  simp only
  rw [â† inf_idem (x =á´® y), inf_assoc]
  nth_grw 1 [hg x y, hf x y]
  grw [eq_symm (f x) (g x), eq_trans', eq_symm (g x) (f y), eq_trans']

@[fun_prop] theorem IsExtentional.mem {f g : BVSet B â†’ BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) : IsExtentional Î» x => f x âˆˆá´® g x := Î» x y => by
  simp only
  rw [â† inf_idem (x =á´® y), inf_assoc]
  nth_grw 1 [hg x y, hf x y]
  grw [mem_congr_left, mem_congr_right]

@[fun_prop] theorem IsExtentional.sup {f g : BVSet B â†’ B}
    (hf : IsExtentional f) (hg : IsExtentional g) : IsExtentional Î» x => f x âŠ” g x := Î» x y => by
  simp only [inf_sup_left, sup_le_iff]
  constructor
  Â· exact (hf x y).trans le_sup_left
  Â· exact (hg x y).trans le_sup_right

@[fun_prop] theorem IsExtentional.inf {f g : BVSet B â†’ B}
    (hf : IsExtentional f) (hg : IsExtentional g) : IsExtentional Î» x => f x âŠ“ g x := Î» x y => by
  simp only [le_inf_iff]
  constructor
  Â· nth_grw 2 [inf_le_left]
    apply hf
  Â· nth_grw 2 [inf_le_right]
    apply hg

@[fun_prop] theorem IsExtentional.compl {f : BVSet B â†’ B} (hf : IsExtentional f) :
    IsExtentional Î» x => (f x)á¶œ := Î» x y => by
  simp only
  rw [â† le_himp_iff, compl_himp_compl, le_himp_iff, eq_symm]
  apply hf

@[fun_prop] theorem IsExtentional.himp {f g : BVSet B â†’ B}
    (hf : IsExtentional f) (hg : IsExtentional g) : IsExtentional Î» x => f x â‡¨ g x := by
  simp_rw [himp_eq]
  fun_prop

@[fun_prop] theorem IsExtentional.iInf {Î± : Sort*} {f : Î± â†’ BVSet B â†’ B}
    (hf : âˆ€ x, IsExtentional (f x)) : IsExtentional Î» x => â¨… y, f y x := Î» x y => by
  simp only [le_iInf_iff]
  intro z
  grw [iInf_le _ z]
  apply hf

@[fun_prop] theorem IsExtentional.iSup {Î± : Sort*} {f : Î± â†’ BVSet B â†’ B}
    (hf : âˆ€ x, IsExtentional (f x)) : IsExtentional Î» x => â¨† y, f y x := Î» x y => by
  simp only [inf_iSup_eq, iSup_le_iff]
  intro z
  exact (hf _ _ _).trans <| le_iSup (Î» z => f z y) z

theorem IsExtentional.iSup_eq_inf {f : BVSet B â†’ B} (hf : IsExtentional f) :
    â¨† x : BVSet B, x =á´® u âŠ“ f x = f u := by
  apply le_antisymm
  Â· rw [iSup_le_iff]
    intro x
    apply hf
  Â· apply le_iSup_of_le u
    simp [BVSet.eq_refl]

theorem IsExtentional.iInf_eq_himp {f : BVSet B â†’ B} (hf : IsExtentional f) :
    â¨… x : BVSet B, x =á´® u â‡¨ f x = f u := by
  apply le_antisymm
  Â· apply iInf_le_of_le u
    simp [BVSet.eq_refl]
  Â· rw [le_iInf_iff]
    intro v
    rw [le_himp_iff', BVSet.eq_symm]
    apply hf

theorem IsExtentional.iSup_mem_inf {f : BVSet B â†’ B} (hf : IsExtentional f) :
    â¨† x : BVSet B, x âˆˆá´® u âŠ“ f x = â¨† x : u, u x âŠ“ f x := by
  simp_rw [BVSet.mem_def, iSup_inf_eq]
  rw [iSup_comm]
  simp_rw [inf_assoc, â† fun j => inf_iSup_eq (u j) fun i => i =á´® j âŠ“ f i, hf.iSup_eq_inf]

theorem IsExtentional.iInf_mem_himp {f : BVSet B â†’ B} (hf : IsExtentional f) :
    â¨… x : BVSet B, x âˆˆá´® u â‡¨ f x = â¨… x : u, u x â‡¨ f x := by
  simp_rw [BVSet.mem_def, iSup_himp_eq]
  rw [iInf_comm]
  simp_rw [â† himp_himp, â† himp_iInf_eq, hf.iInf_eq_himp]

theorem subset_def' : u âŠ†á´® v = â¨… x : BVSet B, x âˆˆá´® u â‡¨ x âˆˆá´® v := by
  rw [subset_def, IsExtentional.iInf_mem_himp (by fun_prop)]

@[fun_prop] theorem IsExtentional.subset {f g : BVSet B â†’ BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) : IsExtentional Î» x => f x âŠ†á´® g x := by
  simp only [subset_def']
  refine .iInf Î» x => ?_
  fun_prop

theorem subset_congr_left : u =á´® v âŠ“ u âŠ†á´® w â‰¤ v âŠ†á´® w := by
  have : IsExtentional Î» x => x âŠ†á´® w := by fun_prop
  apply this

theorem subset_congr_right : v =á´® w âŠ“ u âŠ†á´® v â‰¤ u âŠ†á´® w := by
  have : IsExtentional Î» x => u âŠ†á´® x := by fun_prop
  apply this

theorem IsExtentionalFun.of_isExtentional {f : BVSet B â†’ BVSet B}
    (h : âˆ€ y, IsExtentional Î» x => y âˆˆá´® f x) : IsExtentionalFun f := by
  intro x y
  conv_rhs => rw [BVSet.eq_def]
  simp only [subset_def', le_inf_iff, le_iInf_iff, le_himp_iff]
  constructor
  Â· intro z
    apply h
  Â· intro z
    rw [eq_symm]
    apply h

theorem mem_inf_subset_le (u v w : BVSet B) : u âˆˆá´® v âŠ“ v âŠ†á´® w â‰¤ u âˆˆá´® w := by
  grw [subset_def', iInf_le _ u, inf_himp_le]

theorem subset_inf_mem_le (u v w : BVSet B) : v âŠ†á´® w âŠ“ u âˆˆá´® v â‰¤ u âˆˆá´® w := by
  rw [inf_comm]
  apply mem_inf_subset_le

theorem subset_refl (u) : u âŠ†á´® u = (âŠ¤ : B) := by
  simp [subset_def']

theorem subset_trans (u v w : BVSet B) : u âŠ†á´® v âŠ“ v âŠ†á´® w â‰¤ u âŠ†á´® w := by
  simp only [subset_def', le_iInf_iff, le_himp_iff]
  intro x
  grw [iInf_le _ x, iInf_le _ x, inf_right_comm, himp_inf_le, inf_himp_le]

theorem subset_trans' (u v w : BVSet B) : v âŠ†á´® w âŠ“ u âŠ†á´® v â‰¤ u âŠ†á´® w := by
  rw [inf_comm]
  apply subset_trans


instance : Setoid (BVSet B) where
  r u v := u =á´® v = âŠ¤
  iseqv.refl u := by simp [eq_refl]
  iseqv.symm h := by simpa [eq_symm]
  iseqv.trans hâ‚ hâ‚‚ := by
    grw [eq_top_iff, â† eq_trans, hâ‚, hâ‚‚, top_inf_eq]

theorem equiv_def : u â‰ˆ v â†” u =á´® v = âŠ¤ := Iff.rfl

@[refl] theorem equiv_refl (u : BVSet B) : u â‰ˆ u := IsEquiv.toIsPreorder.refl _

@[symm] theorem equiv_symm : u â‰ˆ v â†’ v â‰ˆ u := IsEquiv.toIsSymm.symm _ _

@[trans] theorem equiv_trans : u â‰ˆ v â†’ v â‰ˆ w â†’ u â‰ˆ w := IsEquiv.toIsPreorder.trans _ _ _

theorem ext (h : âˆ€ x, x âˆˆá´® u = x âˆˆá´® v) : u â‰ˆ v := by
  rw [equiv_def]
  simp [eq_def, subset_def', h]

theorem IsExtentionalFun.congr {f} (hf : IsExtentionalFun f) (h : u â‰ˆ v) : f u â‰ˆ f v := by
  grw [equiv_def, eq_top_iff, â† hf u v, â† eq_top_iff]
  exact h

theorem IsExtentional.congr {f} (hf : IsExtentional f) (h : u â‰ˆ v) : f u = f v := by
  apply le_antisymm
  Â· grw [â† hf u v]
    simp [equiv_def.1 h]
  Â· grw [â† hf v u]
    simp [equiv_def.1 (equiv_symm h)]

@[gcongr] theorem mem_congr {uâ‚ uâ‚‚ vâ‚ vâ‚‚ : BVSet B} (hâ‚ : uâ‚ â‰ˆ uâ‚‚) (hâ‚‚ : vâ‚ â‰ˆ vâ‚‚) :
    uâ‚ âˆˆá´® vâ‚ = uâ‚‚ âˆˆá´® vâ‚‚ := by
  trans uâ‚‚ âˆˆá´® vâ‚
  Â· exact IsExtentional.congr (f := (Â· âˆˆá´® vâ‚)) (by fun_prop) hâ‚
  Â· exact IsExtentional.congr (by fun_prop) hâ‚‚

@[gcongr] theorem eq_congr {uâ‚ uâ‚‚ vâ‚ vâ‚‚ : BVSet B} (hâ‚ : uâ‚ â‰ˆ uâ‚‚) (hâ‚‚ : vâ‚ â‰ˆ vâ‚‚) :
    uâ‚ =á´® vâ‚ = uâ‚‚ =á´® vâ‚‚ := by
  trans uâ‚‚ =á´® vâ‚
  Â· exact IsExtentional.congr (f := (Â· =á´® vâ‚)) (by fun_prop) hâ‚
  Â· exact IsExtentional.congr (by fun_prop) hâ‚‚

@[gcongr] theorem subset_congr {uâ‚ uâ‚‚ vâ‚ vâ‚‚ : BVSet B} (hâ‚ : uâ‚ â‰ˆ uâ‚‚) (hâ‚‚ : vâ‚ â‰ˆ vâ‚‚) :
    uâ‚ âŠ†á´® vâ‚ = uâ‚‚ âŠ†á´® vâ‚‚ := by
  trans uâ‚‚ âŠ†á´® vâ‚
  Â· exact IsExtentional.congr (f := (Â· âŠ†á´® vâ‚)) (by fun_prop) hâ‚
  Â· exact IsExtentional.congr (by fun_prop) hâ‚‚



def empty : BVSet B :=
  âŸ¨PEmpty, nofun, nofunâŸ©

instance : EmptyCollection (BVSet B) := âŸ¨emptyâŸ©
instance : Nonempty (BVSet B) := âŸ¨âˆ…âŸ©

@[simp] theorem mem_empty : u âˆˆá´® âˆ… = âŠ¥ := by
  simp [EmptyCollection.emptyCollection, empty, mem_def]

@[simp] theorem empty_subset : âˆ… âŠ†á´® u = âŠ¤ := by
  simp [subset_def']

protected def insert (u v : BVSet.{u, v} B) : BVSet B :=
  âŸ¨Option v.Index, (Â·.elim u v.dom), (Â·.elim âŠ¤ v.val)âŸ©

instance : Insert (BVSet B) (BVSet B) := âŸ¨BVSet.insertâŸ©

@[simp] theorem mem_insert : u âˆˆá´® insert v w = u =á´® v âŠ” u âˆˆá´® w := by
  simp [insert, BVSet.insert, mem_def, iSup_option]

theorem mem_insert_self : u âˆˆá´® insert u v = âŠ¤ := by
  simp [eq_refl]

theorem le_subset_insert : u âŠ†á´® w â‰¤ u âŠ†á´® insert v w := by
  simp only [subset_def', mem_insert, le_iInf_iff, le_himp_iff]
  intro x
  grw [iInf_le _ x, himp_inf_le, â† le_sup_right]

@[fun_prop] theorem IsExtentionalFun.insert {f g : BVSet B â†’ BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) : IsExtentionalFun Î» x => insert (f x) (g x) := by
  apply of_isExtentional
  intro x
  simp only [mem_insert]
  fun_prop

@[gcongr] theorem insert_congr {uâ‚ uâ‚‚ vâ‚ vâ‚‚ : BVSet B} (hâ‚ : uâ‚ â‰ˆ uâ‚‚) (hâ‚‚ : vâ‚ â‰ˆ vâ‚‚) :
    insert uâ‚ vâ‚ â‰ˆ insert uâ‚‚ vâ‚‚ := by
  trans insert uâ‚‚ vâ‚
  Â· apply IsExtentionalFun.congr _ hâ‚
    fun_prop
  Â· apply IsExtentionalFun.congr _ hâ‚‚
    fun_prop

instance : Singleton (BVSet B) (BVSet B) := âŸ¨(insert Â· âˆ…)âŸ©

@[simp] theorem mem_singleton : u âˆˆá´® {v} = u =á´® v := by
  simp [Singleton.singleton]

@[fun_prop] theorem IsExtentionalFun.singleton {f : BVSet B â†’ BVSet B}
    (hf : IsExtentionalFun f) : IsExtentionalFun Î» x => {f x} := by
  apply of_isExtentional
  intro x
  simp only [mem_singleton]
  fun_prop

@[gcongr] theorem singleton_congr {u v : BVSet B} (h : u â‰ˆ v) : ({u} : BVSet B) â‰ˆ {v} := by
  apply IsExtentionalFun.congr _ h
  fun_prop

def sUnion (u : BVSet.{u, v} B) : BVSet B :=
  âŸ¨Î£ x : u, (x : BVSet B).Index, fun âŸ¨_, yâŸ© => y, fun âŸ¨x, yâŸ© => u x âŠ“ (x : BVSet B) yâŸ©

prefix:110 "â‹ƒá´® " => sUnion

theorem mem_sUnion' : u âˆˆá´® â‹ƒá´® v = â¨† x : v, v x âŠ“ u âˆˆá´® x := by
  conv_lhs => simp only [sUnion, mem_def, Index_mk, val_mk, dom_mk]
  simp only [iSup_sigma]
  simp_rw [inf_assoc, â† inf_iSup_eq, â† mem_def]

@[simp] theorem mem_sUnion : u âˆˆá´® â‹ƒá´® v = â¨† x, x âˆˆá´® v âŠ“ u âˆˆá´® x := by
  rw [mem_sUnion', IsExtentional.iSup_mem_inf (by fun_prop)]

@[fun_prop] theorem IsExtentionalFun.sUnion {f : BVSet B â†’ BVSet B}
    (hf : IsExtentionalFun f) : IsExtentionalFun Î» x => â‹ƒá´® (f x) := by
  apply of_isExtentional
  intro x
  simp only [mem_sUnion]
  fun_prop

@[gcongr] theorem sUnion_congr {u v : BVSet B} (h : u â‰ˆ v) : â‹ƒá´® u â‰ˆ â‹ƒá´® v := by
  apply IsExtentionalFun.congr _ h
  fun_prop

def powerset (u : BVSet.{u, max u v} B) : BVSet.{u, max u v} B :=
  âŸ¨u.Index â†’ B, fun f => âŸ¨u.Index, u.dom, fâŸ©, fun f => âŸ¨u.Index, u.dom, fâŸ© âŠ†á´® uâŸ©

prefix:110 "ð’«á´® " => powerset

@[simp] theorem mem_powerset : u âˆˆá´® ð’«á´® v = u âŠ†á´® v := by
  simp only [powerset, mem_def, Index_mk, val_mk, dom_mk]
  apply le_antisymm
  Â· rw [iSup_le_iff]
    intro f
    rw [inf_comm, BVSet.eq_symm]
    simpa using subset_congr_left
  Â· apply le_iSup_of_le fun x : v => (x : BVSet B) âˆˆá´® u
    rw [le_inf_iff]
    constructor
    Â· conv_rhs => simp only [subset_def, Index_mk, val_mk, dom_mk]
      rw [le_iInf_iff]
      intro x
      rw [subset_def']
      exact iInf_le _ (x : BVSet B)
    Â· rw [eq_def, le_inf_iff]
      constructor
      Â· simp only [subset_def']
        refine iInf_mono fun x => ?_
        simp only [le_himp_iff, himp_inf_self]
        conv_lhs => arg 1; rw [mem_def]
        conv_rhs => rw [mem_def]; simp only [Index_mk, val_mk, dom_mk]
        rw [iSup_inf_eq]
        refine iSup_mono fun y => ?_
        rw [inf_right_comm, le_inf_iff]
        constructor
        Â· rw [inf_assoc]
          apply inf_le_of_right_le
          rw [inf_comm]
          apply mem_congr_left
        Â· simp
      Â· simp [subset_def]

@[fun_prop] theorem IsExtentionalFun.powerset {f : BVSet B â†’ BVSet B}
    (hf : IsExtentionalFun f) : IsExtentionalFun Î» x => ð’«á´® (f x) := by
  apply of_isExtentional
  intro x
  simp only [mem_powerset]
  fun_prop

@[gcongr] theorem powerset_congr {u v : BVSet B} (h : u â‰ˆ v) : ð’«á´® u â‰ˆ ð’«á´® v := by
  apply IsExtentionalFun.congr _ h
  fun_prop

def sep (u : BVSet B) (f : BVSet B â†’ B) : BVSet B :=
  âŸ¨u.Index, fun i => i, fun i => u i âŠ“ f iâŸ©

theorem mem_sep {f} : u âˆˆá´® sep v f = â¨† x : v, v x âŠ“ u =á´® x âŠ“ f x := by
  simp only [sep, mem_def, Index_mk, val_mk, dom_mk]
  ac_rfl

theorem mem_sep' {f} (hf : IsExtentional f) : u âˆˆá´® sep v f = u âˆˆá´® v âŠ“ f u := by
  simp only [sep, mem_def, Index_mk, val_mk, dom_mk, iSup_inf_eq]
  congr! 2 with i
  rw [inf_assoc, inf_assoc]
  congr 1
  apply le_antisymm
  Â· simp only [le_inf_iff, inf_le_right, true_and]
    rw [inf_comm, eq_symm]
    apply hf
  Â· simp only [le_inf_iff, inf_le_left, and_true]
    apply hf

@[gcongr] theorem sep_congr {f} (h : u â‰ˆ v) (hf : IsExtentional f) : sep u f â‰ˆ sep v f := by
  apply ext
  intro x
  grw [mem_sep' hf, mem_sep' hf, h]

def replace (u : BVSet B) (f : BVSet B â†’ BVSet B) : BVSet B :=
  âŸ¨u.Index, fun i => f i, fun i => u iâŸ©

theorem mem_replace {f} : u âˆˆá´® replace v f = â¨† x : v, v x âŠ“ u =á´® f x := by
  simp [replace, mem_def]
  
theorem mem_replace' {f} (hf : IsExtentionalFun f) : u âˆˆá´® replace v f = â¨† x : BVSet B, x âˆˆá´® v âŠ“ u =á´® f x := by
  rw [mem_replace, IsExtentional.iSup_mem_inf (by fun_prop)]

@[gcongr] theorem sep_replace {f} (h : u â‰ˆ v) (hf : IsExtentionalFun f) : replace u f â‰ˆ replace v f := by
  apply ext
  intro x
  rw [mem_replace' hf, mem_replace' hf]
  congr! 2 with y
  grw [h]

end BVSet

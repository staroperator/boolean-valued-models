import BooleanValuedModels.BVSet.Ordinal
import BooleanValuedModels.BooleanAlgebra.CountableChainCondition
import BooleanValuedModels.DeltaSystemLemma
import Mathlib.SetTheory.ZFC.Cardinal

@[simp]
theorem Ordinal.card_toZFSet (o : Ordinal) : o.toZFSet.card = o.card := by
  simpa [‚Üê coe_toZFSet, ZFSet.cardinalMk_coe_sort, mk_Iio_ordinal, ‚Üê lift_card] using
    Cardinal.mk_image_eq (s := Set.Iio o) toZFSet_injective

namespace BVSet

universe u v

variable {B : Type u} [CompleteBooleanAlgebra B] {u v w : BVSet.{u, v} B} {x y : ZFSet.{v}}

def kpair (u v : BVSet B) : BVSet B :=
  {{u}, {u, v}}

@[fun_prop] protected theorem IsExtentionalFun.kpair {f g : BVSet B ‚Üí BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) :
    IsExtentionalFun fun x => kpair (f x) (g x) := by
  unfold kpair
  fun_prop

@[gcongr] theorem kpair_congr {u‚ÇÅ u‚ÇÇ v‚ÇÅ v‚ÇÇ : BVSet B} (h‚ÇÅ : u‚ÇÅ ‚âà u‚ÇÇ) (h‚ÇÇ : v‚ÇÅ ‚âà v‚ÇÇ) :
    kpair u‚ÇÅ v‚ÇÅ ‚âà kpair u‚ÇÇ v‚ÇÇ := by
  trans kpair u‚ÇÇ v‚ÇÅ
  ¬∑ apply IsExtentionalFun.congr _ h‚ÇÅ
    fun_prop
  ¬∑ apply IsExtentionalFun.congr _ h‚ÇÇ
    fun_prop

theorem kpair_eq_kpair {u‚ÇÅ u‚ÇÇ v‚ÇÅ v‚ÇÇ : BVSet B} :
    kpair u‚ÇÅ v‚ÇÅ =·¥Æ kpair u‚ÇÇ v‚ÇÇ = u‚ÇÅ =·¥Æ u‚ÇÇ ‚äì v‚ÇÅ =·¥Æ v‚ÇÇ := by
  apply le_antisymm
  ¬∑ apply le_of_inf_le (u‚ÇÅ =·¥Æ u‚ÇÇ)
    ¬∑ grw [eq_le_subset, subset_def', iInf_le _ {u‚ÇÅ}]
      simp [kpair]
    ¬∑ apply le_inf
      ¬∑ grw [inf_le_right]
      ¬∑ grw [IsExtentional.inf_eq_le' (kpair u‚ÇÅ v‚ÇÅ =·¥Æ kpair ¬∑ v‚ÇÇ) (by fun_prop)]
        apply le_of_inf_le (u‚ÇÅ =·¥Æ v‚ÇÅ ‚á® v‚ÇÅ =·¥Æ v‚ÇÇ)
        ¬∑ rw [le_himp_iff]
          grw [IsExtentional.inf_eq_le (fun u => kpair u v‚ÇÅ =·¥Æ kpair u v‚ÇÇ) (by fun_prop)]
          simp [kpair]
        ¬∑ simp only [kpair, pair_eq_pair, eq_refl, le_top, inf_of_le_right, singleton_eq_pair,
            le_sup_right, sup_of_le_left, inf_sup_right, le_himp_iff, inf_le_left, inf_of_le_left,
            sup_le_iff, le_refl, true_and]
          grw [inf_assoc, inf_himp_le, inf_le_right]
  ¬∑ have : IsExtentionalFun‚ÇÇ (B := B) kpair := by
      apply IsExtentionalFun‚ÇÇ.of_isExtentionalFun <;> fun_prop
    apply this

lemma le_kpair_mem [Small.{v} B] :
    u ‚àà·¥Æ w ‚äì v ‚àà·¥Æ w ‚â§ kpair u v ‚àà·¥Æ ùí´·¥Æ ùí´·¥Æ w := by
  simp [kpair]

theorem _root_.ZFSet.toBVSet_pair :
    (x.pair y).toBVSet ‚âà kpair (x.toBVSet : BVSet B) y.toBVSet := by
  simp only [ZFSet.pair, kpair]
  grw [ZFSet.toBVSet_insert, ZFSet.toBVSet_singleton, ZFSet.toBVSet_singleton, ZFSet.toBVSet_insert,
    ZFSet.toBVSet_singleton]

noncomputable def prod [Small.{v} B] (u v : BVSet.{u, v} B) : BVSet B :=
  (ùí´·¥Æ ùí´·¥Æ (u ‚à™ v)).sep fun x => ‚®Ü y, y ‚àà·¥Æ u ‚äì ‚®Ü z, z ‚àà·¥Æ v ‚äì x =·¥Æ kpair y z

@[fun_prop] protected theorem IsExtentionalFun.prod [Small.{v} B] {f g : BVSet B ‚Üí BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) :
    IsExtentionalFun fun x => prod (f x) (g x) := by
  unfold prod
  fun_prop

@[gcongr] theorem prod_congr [Small.{v} B] {u‚ÇÅ u‚ÇÇ v‚ÇÅ v‚ÇÇ : BVSet B} (h‚ÇÅ : u‚ÇÅ ‚âà u‚ÇÇ) (h‚ÇÇ : v‚ÇÅ ‚âà v‚ÇÇ) :
    prod u‚ÇÅ v‚ÇÅ ‚âà prod u‚ÇÇ v‚ÇÇ := by
  trans prod u‚ÇÇ v‚ÇÅ
  ¬∑ apply IsExtentionalFun.congr _ h‚ÇÅ
    fun_prop
  ¬∑ apply IsExtentionalFun.congr _ h‚ÇÇ
    fun_prop

theorem mem_prod [Small.{v} B] :
    u ‚àà·¥Æ prod v w = ‚®Ü x, x ‚àà·¥Æ v ‚äì ‚®Ü y, y ‚àà·¥Æ w ‚äì u =·¥Æ kpair x y := by
  simp [prod]
  rw [mem_sep (by fun_prop), inf_eq_right]
  apply iSup_le
  intro x
  rw [inf_iSup_eq]
  apply iSup_le
  intro y
  grw [‚Üê inf_assoc, ‚Üê IsExtentional.eq_inf_le' (¬∑ ‚àà·¥Æ _) (by fun_prop) (kpair x y) u]
  apply le_inf
  ¬∑ grw [inf_le_right]
  ¬∑ grw [inf_le_left, ‚Üê le_kpair_mem]
    apply inf_le_inf
    ¬∑ grw [‚Üê subset_inf_mem_le x v (v ‚à™ w)]
      simp
    ¬∑ grw [‚Üê subset_inf_mem_le y w (v ‚à™ w)]
      simp

theorem le_kpair_mem_prod [Small.{v} B] {x y} :
    x ‚àà·¥Æ u ‚äì y ‚àà·¥Æ v ‚â§ kpair x y ‚àà·¥Æ prod u v := by
  rw [mem_prod]
  refine le_iSup_of_le x (le_inf ?_ (le_iSup_of_le y (le_inf ?_ ?_)))
  ¬∑ grw [inf_le_left]
  ¬∑ grw [inf_le_right]
  ¬∑ simp

theorem _root_.ZFSet.toBVSet_prod [Small.{v} B] :
    (x.prod y).toBVSet ‚âà x.toBVSet.prod (y.toBVSet : BVSet B) := by
  apply BVSet.ext
  intro u
  apply le_antisymm
  ¬∑ rw [ZFSet.mem_toBVSet]
    apply iSup_le
    intro ‚ü®z, hz‚ü©
    simp only [ZFSet.mem_prod] at hz
    rcases hz with ‚ü®z‚ÇÅ, hz‚ÇÅ, z‚ÇÇ, hz‚ÇÇ, rfl‚ü©
    rw [mem_prod, IsExtentional.iSup_mem_toBVSet_inf (by fun_prop)]
    apply le_iSup_of_le ‚ü®z‚ÇÅ, hz‚ÇÅ‚ü©
    rw [IsExtentional.iSup_mem_toBVSet_inf (by fun_prop)]
    apply le_iSup_of_le ‚ü®z‚ÇÇ, hz‚ÇÇ‚ü©
    simp only
    grw [ZFSet.toBVSet_pair]
  ¬∑ rw [mem_prod, IsExtentional.iSup_mem_toBVSet_inf (by fun_prop)]
    apply iSup_le
    intro ‚ü®z‚ÇÅ, hz‚ÇÅ‚ü©
    rw [IsExtentional.iSup_mem_toBVSet_inf (by fun_prop)]
    apply iSup_le
    intro ‚ü®z‚ÇÇ, hz‚ÇÇ‚ü©
    rw [ZFSet.mem_toBVSet]
    apply le_iSup_of_le ‚ü®z‚ÇÅ.pair z‚ÇÇ, by simp [hz‚ÇÅ, hz‚ÇÇ]‚ü©
    simp only
    grw [ZFSet.toBVSet_pair]

def isFunc [Small.{v} B] (u v f : BVSet B) :=
  f ‚äÜ·¥Æ prod u v ‚äì (‚®Ö x, x ‚àà·¥Æ u ‚á® ‚®Ü y, y ‚àà·¥Æ v ‚äì kpair x y ‚àà·¥Æ f)
    ‚äì ‚®Ö x, x ‚àà·¥Æ u ‚á® ‚®Ö y‚ÇÅ, y‚ÇÅ ‚àà·¥Æ v ‚á® ‚®Ö y‚ÇÇ, y‚ÇÇ ‚àà·¥Æ v ‚á® kpair x y‚ÇÅ ‚àà·¥Æ f ‚á® kpair x y‚ÇÇ ‚àà·¥Æ f ‚á® y‚ÇÅ =·¥Æ y‚ÇÇ

@[fun_prop] protected theorem IsExtentional.isFunc [Small.{v} B] {f g h : BVSet B ‚Üí BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) (hh : IsExtentionalFun h) :
    IsExtentional fun x => isFunc (f x) (g x) (h x) := by
  unfold isFunc
  fun_prop

@[gcongr] theorem isFunc_congr [Small.{v} B] {u‚ÇÅ u‚ÇÇ v‚ÇÅ v‚ÇÇ f‚ÇÅ f‚ÇÇ : BVSet B}
    (h‚ÇÅ : u‚ÇÅ ‚âà u‚ÇÇ) (h‚ÇÇ : v‚ÇÅ ‚âà v‚ÇÇ) (h‚ÇÉ : f‚ÇÅ ‚âà f‚ÇÇ) :
    isFunc u‚ÇÅ v‚ÇÅ f‚ÇÅ = isFunc u‚ÇÇ v‚ÇÇ f‚ÇÇ := by
  trans isFunc u‚ÇÇ v‚ÇÅ f‚ÇÅ
  ¬∑ apply IsExtentional.congr _ h‚ÇÅ
    fun_prop
  trans isFunc u‚ÇÇ v‚ÇÇ f‚ÇÅ
  ¬∑ apply IsExtentional.congr _ h‚ÇÇ
    fun_prop
  ¬∑ apply IsExtentional.congr _ h‚ÇÉ
    fun_prop

theorem isFunc_total [Small.{v} B] {u v f : BVSet B} :
    isFunc u v f ‚â§ ‚®Ö x, x ‚àà·¥Æ u ‚á® ‚®Ü y, y ‚àà·¥Æ v ‚äì kpair x y ‚àà·¥Æ f :=
  inf_le_of_left_le inf_le_right

theorem isFunc_total' [Small.{v} B] {u v f x : BVSet B} :
    isFunc u v f ‚äì x ‚àà·¥Æ u ‚â§ ‚®Ü y, y ‚àà·¥Æ v ‚äì kpair x y ‚àà·¥Æ f := by
  grw [isFunc_total, iInf_le _ x, himp_inf_le]

theorem isFunc_unique [Small.{v} B] {u v f : BVSet B} :
    isFunc u v f ‚â§ ‚®Ö x, x ‚àà·¥Æ u ‚á® ‚®Ö y‚ÇÅ, y‚ÇÅ ‚àà·¥Æ v ‚á® ‚®Ö y‚ÇÇ, y‚ÇÇ ‚àà·¥Æ v ‚á® kpair x y‚ÇÅ ‚àà·¥Æ f ‚á® kpair x y‚ÇÇ ‚àà·¥Æ f ‚á® y‚ÇÅ =·¥Æ y‚ÇÇ :=
  inf_le_right

theorem isFunc_unique' [Small.{v} B] {u v f x y‚ÇÅ y‚ÇÇ : BVSet B} :
    isFunc u v f ‚äì x ‚àà·¥Æ u ‚äì y‚ÇÅ ‚àà·¥Æ v ‚äì y‚ÇÇ ‚àà·¥Æ v ‚äì kpair x y‚ÇÅ ‚àà·¥Æ f ‚äì kpair x y‚ÇÇ ‚àà·¥Æ f ‚â§ y‚ÇÅ =·¥Æ y‚ÇÇ := by
  grw [isFunc_unique, iInf_le _ x, himp_inf_le, iInf_le _ y‚ÇÅ, himp_inf_le, iInf_le _ y‚ÇÇ, himp_inf_le, himp_inf_le, himp_inf_le]

theorem _root_.ZFSet.isFunc_toBVSet_of_isFunc [Small.{v} B] {f : ZFSet} (h : ZFSet.IsFunc x y f) :
    isFunc x.toBVSet y.toBVSet f.toBVSet = (‚ä§ : B) := by
  unfold isFunc
  rw [inf_eq_top_iff, inf_eq_top_iff]
  refine ‚ü®‚ü®?_, ?_‚ü©, ?_‚ü©
  ¬∑ grw [‚Üê ZFSet.toBVSet_prod]
    rw [ZFSet.toBVSet_subset_toBVSet_of_subset h.1]
  ¬∑ rw [IsExtentional.iInf_mem_toBVSet_himp (by fun_prop), iInf_eq_top]
    intro ‚ü®a, ha‚ü©
    rw [IsExtentional.iSup_mem_toBVSet_inf (by fun_prop), eq_top_iff]
    rcases h.2 a ha with ‚ü®b, hb, -‚ü©
    have hb' := h.1 hb
    simp only [ZFSet.mem_prod, ZFSet.pair_inj, exists_eq_right_right'] at hb'
    apply le_iSup_of_le ‚ü®b, hb'.2‚ü©
    simp only [top_le_iff]
    grw [‚Üê ZFSet.toBVSet_pair, ZFSet.toBVSet_mem_toBVSet_of_mem hb]
  ¬∑ rw [IsExtentional.iInf_mem_toBVSet_himp (by fun_prop), iInf_eq_top]
    intro ‚ü®a, ha‚ü©
    rw [IsExtentional.iInf_mem_toBVSet_himp (by fun_prop), iInf_eq_top]
    intro ‚ü®b‚ÇÅ, hb‚ÇÅ‚ü©
    rw [IsExtentional.iInf_mem_toBVSet_himp (by fun_prop), iInf_eq_top]
    intro ‚ü®b‚ÇÇ, hb‚ÇÇ‚ü©
    simp only [himp_eq_top_iff, le_himp_iff, ge_iff_le]
    grw [‚Üê ZFSet.toBVSet_pair, ‚Üê ZFSet.toBVSet_pair]
    by_cases h‚ÇÅ : a.pair b‚ÇÅ ‚àà f
    ¬∑ by_cases h‚ÇÇ : a.pair b‚ÇÇ ‚àà f
      ¬∑ simp [(h.2 a ha).unique h‚ÇÅ h‚ÇÇ]
      ¬∑ simp [ZFSet.toBVSet_mem_toBVSet_of_notMem h‚ÇÇ]
    ¬∑ simp [ZFSet.toBVSet_mem_toBVSet_of_notMem h‚ÇÅ]

def isInjective (u v f : BVSet B) :=
  ‚®Ö x‚ÇÅ, x‚ÇÅ ‚àà·¥Æ u ‚á® ‚®Ö x‚ÇÇ, x‚ÇÇ ‚àà·¥Æ u ‚á® ‚®Ö y, y ‚àà·¥Æ v ‚á® kpair x‚ÇÅ y ‚àà·¥Æ f ‚á® kpair x‚ÇÇ y ‚àà·¥Æ f ‚á® x‚ÇÅ =·¥Æ x‚ÇÇ

@[fun_prop] protected theorem IsExtentional.isInjective {f g h : BVSet B ‚Üí BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) (hh : IsExtentionalFun h) :
    IsExtentional fun x => isInjective (f x) (g x) (h x) := by
  unfold isInjective
  fun_prop

@[gcongr] theorem isInjective_congr {u‚ÇÅ u‚ÇÇ v‚ÇÅ v‚ÇÇ f‚ÇÅ f‚ÇÇ : BVSet B} (h‚ÇÅ : u‚ÇÅ ‚âà u‚ÇÇ) (h‚ÇÇ : v‚ÇÅ ‚âà v‚ÇÇ) (h‚ÇÉ : f‚ÇÅ ‚âà f‚ÇÇ) :
    isInjective u‚ÇÅ v‚ÇÅ f‚ÇÅ = isInjective u‚ÇÇ v‚ÇÇ f‚ÇÇ := by
  trans isInjective u‚ÇÇ v‚ÇÅ f‚ÇÅ
  ¬∑ apply IsExtentional.congr _ h‚ÇÅ
    fun_prop
  trans isInjective u‚ÇÇ v‚ÇÇ f‚ÇÅ
  ¬∑ apply IsExtentional.congr _ h‚ÇÇ
    fun_prop
  ¬∑ apply IsExtentional.congr _ h‚ÇÉ
    fun_prop

theorem _root_.ZFSet.isInjective_toBVSet_of_injOn {f : ZFSet ‚Üí ZFSet} [ZFSet.Definable‚ÇÅ f] (hf : Set.InjOn f x) :
    isInjective x.toBVSet y.toBVSet (ZFSet.map f x).toBVSet = (‚ä§ : B) := by
  rw [eq_top_iff, isInjective, IsExtentional.iInf_mem_toBVSet_himp (by fun_prop)]
  refine le_iInf fun z‚ÇÅ => ?_
  rw [IsExtentional.iInf_mem_toBVSet_himp (by fun_prop)]
  refine le_iInf fun z‚ÇÇ => ?_
  rw [IsExtentional.iInf_mem_toBVSet_himp (by fun_prop)]
  refine le_iInf fun z => ?_
  grw [‚Üê ZFSet.toBVSet_pair, ‚Üê ZFSet.toBVSet_pair]
  by_cases hz‚ÇÅ : z‚ÇÅ.1.pair z ‚àà ZFSet.map f x
  ¬∑ grw [ZFSet.toBVSet_mem_toBVSet_of_mem hz‚ÇÅ, top_himp]
    by_cases hz‚ÇÇ : z‚ÇÇ.1.pair z ‚àà ZFSet.map f x
    ¬∑ grw [ZFSet.toBVSet_mem_toBVSet_of_mem hz‚ÇÇ, top_himp]
      simp only [ZFSet.mem_map, ZFSet.pair_inj, ‚ÜìexistsAndEq, SetLike.coe_mem, true_and] at hz‚ÇÅ hz‚ÇÇ
      simp [Subtype.val_inj.1 (hf z‚ÇÅ.2 z‚ÇÇ.2 (hz‚ÇÅ.trans hz‚ÇÇ.symm))]
    ¬∑ simp [ZFSet.toBVSet_mem_toBVSet_of_notMem hz‚ÇÇ]
  ¬∑ simp [ZFSet.toBVSet_mem_toBVSet_of_notMem hz‚ÇÅ]

def isSurjective (u v f : BVSet B) :=
  ‚®Ö y, y ‚àà·¥Æ v ‚á® ‚®Ü x, x ‚àà·¥Æ u ‚äì kpair x y ‚àà·¥Æ f

@[fun_prop] protected theorem IsExtentional.isSurjective {f g h : BVSet B ‚Üí BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) (hh : IsExtentionalFun h) :
    IsExtentional fun x => isSurjective (f x) (g x) (h x) := by
  unfold isSurjective
  fun_prop

@[gcongr] theorem isSurjective_congr {u‚ÇÅ u‚ÇÇ v‚ÇÅ v‚ÇÇ f‚ÇÅ f‚ÇÇ : BVSet B} (h‚ÇÅ : u‚ÇÅ ‚âà u‚ÇÇ) (h‚ÇÇ : v‚ÇÅ ‚âà v‚ÇÇ) (h‚ÇÉ : f‚ÇÅ ‚âà f‚ÇÇ) :
    isSurjective u‚ÇÅ v‚ÇÅ f‚ÇÅ = isSurjective u‚ÇÇ v‚ÇÇ f‚ÇÇ := by
  trans isSurjective u‚ÇÇ v‚ÇÅ f‚ÇÅ
  ¬∑ apply IsExtentional.congr _ h‚ÇÅ
    fun_prop
  trans isSurjective u‚ÇÇ v‚ÇÇ f‚ÇÅ
  ¬∑ apply IsExtentional.congr _ h‚ÇÇ
    fun_prop
  ¬∑ apply IsExtentional.congr _ h‚ÇÉ
    fun_prop

theorem _root_.ZFSet.isSurjective_toBVSet_of_surjOn {f : ZFSet ‚Üí ZFSet} [ZFSet.Definable‚ÇÅ f] (hf : Set.SurjOn f x y) :
    isSurjective x.toBVSet y.toBVSet (ZFSet.map f x).toBVSet = (‚ä§ : B) := by
  rw [eq_top_iff, isSurjective, IsExtentional.iInf_mem_toBVSet_himp (by fun_prop)]
  refine le_iInf fun z => ?_
  rcases hf z.2 with ‚ü®z', hz', hz‚ü©
  simp only [SetLike.mem_coe] at hz'
  rw [IsExtentional.iSup_mem_toBVSet_inf (by fun_prop)]
  apply le_iSup_of_le ‚ü®z', hz'‚ü©
  grw [‚Üê ZFSet.toBVSet_pair, ZFSet.toBVSet_mem_toBVSet_of_mem]
  simp [hz', hz]

def cardLE [Small.{v} B] (u v : BVSet B) :=
  ‚®Ü f, isFunc u v f ‚äì isInjective u v f

infix:70 " ‚â§·¥Æ " => cardLE

@[fun_prop] protected theorem IsExtentional.cardLE [Small.{v} B] {f g : BVSet B ‚Üí BVSet B}
    (hf : IsExtentionalFun f) (hg : IsExtentionalFun g) :
    IsExtentional fun x => f x ‚â§·¥Æ g x := by
  unfold cardLE
  fun_prop

@[gcongr] theorem cardLE_congr [Small.{v} B] {u‚ÇÅ u‚ÇÇ v‚ÇÅ v‚ÇÇ : BVSet B} (h‚ÇÅ : u‚ÇÅ ‚âà u‚ÇÇ) (h‚ÇÇ : v‚ÇÅ ‚âà v‚ÇÇ) :
    u‚ÇÅ ‚â§·¥Æ v‚ÇÅ = u‚ÇÇ ‚â§·¥Æ v‚ÇÇ := by
  trans u‚ÇÇ ‚â§·¥Æ v‚ÇÅ
  ¬∑ apply IsExtentional.congr _ h‚ÇÅ
    fun_prop
  ¬∑ apply IsExtentional.congr _ h‚ÇÇ
    fun_prop

theorem cardLE_inf_ne_empty_le_isSurjective [Small.{v} B] :
    u ‚â§·¥Æ v ‚äì u ‚â†·¥Æ ‚àÖ ‚â§ ‚®Ü f, isFunc v u f ‚äì isSurjective v u f := by
  simp_rw [cardLE, iSup_inf_eq, ne_empty, inf_iSup_eq]
  refine iSup_le fun f => iSup_le fun x‚ÇÄ => ?_
  let g := sep (prod v u) fun z =>
    ‚®Ü x, x ‚àà·¥Æ u ‚äì ‚®Ü y, y ‚àà·¥Æ v ‚äì z =·¥Æ kpair y x ‚äì (kpair x y ‚àà·¥Æ f ‚äî (x =·¥Æ x‚ÇÄ ‚äì (‚®Ü x', x' ‚àà·¥Æ u ‚äì kpair x' y ‚àà·¥Æ f)·∂ú))
  refine le_iSup_of_le g (le_inf (le_inf (le_inf ?_ ?_) ?_) ?_)
  ¬∑ grw [sep_subset (by fun_prop), ‚Üê le_top]
  ¬∑ refine le_iInf fun y => ?_
    rw [le_himp_iff]
    apply le_of_inf_le_of_compl_le (‚®Ü x', x' ‚àà·¥Æ u ‚äì kpair x' y ‚àà·¥Æ f)
    ¬∑ rw [inf_iSup_eq]
      refine iSup_le fun x => le_iSup_of_le x (le_inf ?_ ?_)
      ¬∑ grw [inf_le_right, inf_le_left]
      ¬∑ rw [mem_sep (by fun_prop)]
        refine le_inf ?_ (le_iSup_of_le x (le_inf ?_ (le_iSup_of_le y (le_inf (le_inf ?_ ?_) ?_))))
        ¬∑ grw [‚Üê le_kpair_mem_prod]
          apply le_inf
          ¬∑ grw [inf_le_left, inf_le_right]
          ¬∑ grw [inf_le_right, inf_le_left]
        ¬∑ grw [inf_le_right, inf_le_left]
        ¬∑ grw [inf_le_left, inf_le_right]
        ¬∑ simp
        ¬∑ apply le_sup_of_le_left
          grw [inf_le_right, inf_le_right]
    ¬∑ refine le_iSup_of_le x‚ÇÄ (le_inf ?_ ?_)
      ¬∑ grw [inf_le_left, inf_le_left, inf_le_right]
      ¬∑ rw [mem_sep (by fun_prop)]
        refine le_inf ?_ (le_iSup_of_le x‚ÇÄ (le_inf ?_ (le_iSup_of_le y (le_inf (le_inf ?_ ?_) ?_))))
        ¬∑ grw [‚Üê le_kpair_mem_prod]
          apply le_inf
          ¬∑ grw [inf_le_left, inf_le_right]
          ¬∑ grw [inf_le_left, inf_le_left, inf_le_right]
        ¬∑ grw [inf_le_left, inf_le_left, inf_le_right]
        ¬∑ grw [inf_le_left, inf_le_right]
        ¬∑ simp
        ¬∑ refine le_sup_of_le_right (le_inf ?_ ?_)
          ¬∑ simp
          ¬∑ grw [inf_le_right]
  ¬∑ refine le_iInf fun y => le_himp_iff.2 (le_iInf fun x‚ÇÅ => le_himp_iff.2 (le_iInf fun x‚ÇÇ => ?_))
    grw [le_himp_iff, le_himp_iff, mem_sep (by fun_prop), inf_le_right (a := kpair _ _ ‚àà·¥Æ _), inf_iSup_eq]
    refine iSup_le fun x‚ÇÅ' => ?_
    grw [inf_le_right (a := _ ‚àà·¥Æ u), inf_iSup_eq]
    refine iSup_le fun y' => ?_
    grw [inf_le_right (a := _ ‚àà·¥Æ v), ‚Üê inf_assoc, inf_right_comm, kpair_eq_kpair, ‚Üê inf_assoc]
    apply IsExtentional.inf_eq_le_of_le' (by fun_prop) (by fun_prop) x‚ÇÅ x‚ÇÅ'
    apply IsExtentional.inf_eq_le_of_le' (by fun_prop) (by fun_prop) y y'
    grw [le_himp_iff, mem_sep (by fun_prop), inf_le_right (a := kpair _ _ ‚àà·¥Æ _), inf_iSup_eq]
    refine iSup_le fun x‚ÇÇ' => ?_
    grw [inf_le_right (a := _ ‚àà·¥Æ u), inf_iSup_eq]
    refine iSup_le fun y'' => ?_
    grw [inf_le_right (a := _ ‚àà·¥Æ v), ‚Üê inf_assoc, inf_right_comm, kpair_eq_kpair, ‚Üê inf_assoc]
    apply IsExtentional.inf_eq_le_of_le' (by fun_prop) (by fun_prop) x‚ÇÇ x‚ÇÇ'
    apply IsExtentional.inf_eq_le_of_le' (by fun_prop) (by fun_prop) y y''
    rw [inf_sup_left, inf_sup_left, inf_sup_right, inf_sup_right]
    refine sup_le (sup_le ?_ ?_) (sup_le ?_ ?_)
    ¬∑ grw [inf_le_left (b := x‚ÇÄ ‚àà·¥Æ u), inf_right_comm _ (y ‚àà·¥Æ v), inf_right_comm _ (y ‚àà·¥Æ v),
        isInjective, iInf_le _ x‚ÇÅ, inf_assoc _ _ (x‚ÇÅ ‚àà·¥Æ u), himp_inf_le, iInf_le _ x‚ÇÇ, inf_assoc _ _ (x‚ÇÇ ‚àà·¥Æ u),
        himp_inf_le, iInf_le _ y, inf_assoc _ _ (y ‚àà·¥Æ v), himp_inf_le, inf_assoc _ _ (kpair x‚ÇÅ _ ‚àà·¥Æ _),
        himp_inf_le, inf_assoc, himp_inf_le, inf_le_right]
    ¬∑ grw [inf_right_comm, ‚Üê inf_assoc, ‚Üê bot_le (a := x‚ÇÅ =·¥Æ x‚ÇÇ), inf_compl_le_bot]
      refine le_iSup_of_le x‚ÇÇ (le_inf ?_ ?_)
      ¬∑ grw [inf_le_left, inf_le_left, inf_le_right]
      ¬∑ grw [inf_le_left, inf_le_right]
    ¬∑ grw [‚Üê inf_assoc, ‚Üê bot_le (a := x‚ÇÅ =·¥Æ x‚ÇÇ), inf_compl_le_bot]
      refine le_iSup_of_le x‚ÇÅ (le_inf ?_ ?_)
      ¬∑ grw [inf_le_left, inf_le_left, inf_le_left, inf_le_right]
      ¬∑ grw [inf_le_left, inf_le_right]
    ¬∑ grw [inf_le_left (a := x‚ÇÅ =·¥Æ x‚ÇÄ), inf_le_left (a := x‚ÇÇ =·¥Æ x‚ÇÄ), inf_assoc, eq_symm x‚ÇÇ x‚ÇÄ, eq_trans, inf_le_right]
  ¬∑ refine le_iInf fun x => le_himp_iff.2 ?_
    grw [inf_le_left (a := isFunc _ _ _), inf_le_left (a := isFunc _ _ _), ‚Üê inf_idem (x ‚àà·¥Æ u), ‚Üê inf_assoc,
      isFunc_total', iSup_inf_eq]
    refine iSup_mono fun y => le_inf ?_ ?_
    ¬∑ grw [inf_le_left, inf_le_left]
    ¬∑ rw [mem_sep (by fun_prop)]
      refine le_inf ?_ (le_iSup_of_le x (le_inf ?_ (le_iSup_of_le y (le_inf (le_inf ?_ ?_) (le_sup_of_le_left ?_)))))
      ¬∑ grw [‚Üê le_kpair_mem_prod, inf_le_left (a := y ‚àà·¥Æ v)]
      ¬∑ grw [inf_le_right]
      ¬∑ grw [inf_le_left, inf_le_left]
      ¬∑ simp
      ¬∑ grw [inf_le_left, inf_le_right]

open Cardinal

theorem _root_.ZFSet.cardLE_toBVSet_of_card_le_card [Small.{v} B] (h : x.card ‚â§ y.card) :
    x.toBVSet ‚â§·¥Æ y.toBVSet = (‚ä§ : B) := by
  classical
  haveI := @Classical.allZFSetDefinable
  rw [‚Üê lift_le, ‚Üê ZFSet.cardinalMk_coe_sort, ‚Üê ZFSet.cardinalMk_coe_sort, le_def] at h
  rcases h with ‚ü®f‚ü©
  let g : ZFSet := ZFSet.map (fun z => if hz : z ‚àà x then f ‚ü®z, hz‚ü© else ‚àÖ) x
  rw [eq_top_iff]
  apply le_iSup_of_le g.toBVSet
  rw [ZFSet.isFunc_toBVSet_of_isFunc, ZFSet.isInjective_toBVSet_of_injOn, top_inf_eq]
  ¬∑ intro z‚ÇÅ hz‚ÇÅ z‚ÇÇ hz‚ÇÇ h
    simp only [SetLike.mem_coe] at hz‚ÇÅ hz‚ÇÇ
    simpa [hz‚ÇÅ, hz‚ÇÇ] using h
  ¬∑ rw [ZFSet.map_isFunc]
    intro z hz
    simp [hz]

theorem _root_.ZFSet.cardLE_toBVSet_of_card_gt_card [Small.{v} B] [CountableChainCondition B]
    (h : y.card < x.card) : x.toBVSet ‚â§·¥Æ y.toBVSet = (‚ä• : B) := by
  have hxne : x.toBVSet ‚â†·¥Æ ‚àÖ = (‚ä§ : B) := by
    grw [‚Üê ZFSet.toBVSet_empty, ZFSet.toBVSet_eq_toBVSet_of_ne, compl_bot]
    rintro rfl
    simp at h
  rw [‚Üê lift_lt, ‚Üê ZFSet.cardinalMk_coe_sort, ‚Üê ZFSet.cardinalMk_coe_sort] at h
  rcases le_or_gt x.card ‚Ñµ‚ÇÄ with hx | hx
  ¬∑ rw [‚Üê lift_le, ‚Üê ZFSet.cardinalMk_coe_sort, lift_aleph0] at hx
    haveI : Finite y := by rw [‚Üê lt_aleph0_iff_finite]; exact h.trans_le hx
    grw [eq_bot_iff, ‚Üê inf_top_eq (_ ‚â§·¥Æ _), ‚Üê hxne, cardLE_inf_ne_empty_le_isSurjective]
    apply iSup_le
    intro f
    rw [‚Üê inf_idem (isFunc _ _ _)]
    nth_grw 2 [isFunc_total]
    rw [IsExtentional.iInf_mem_toBVSet_himp (by fun_prop)]
    conv_lhs => enter [1, 2, 1, z]; rw [IsExtentional.iSup_mem_toBVSet_inf (by fun_prop)]
    rw [iInf_iSup_eq_of_finite, inf_iSup_eq, iSup_inf_eq]
    apply iSup_le
    intro g
    have : ¬¨ g.Surjective := fun hg => (Cardinal.mk_le_of_surjective hg).not_gt h
    simp only [Function.Surjective, not_forall, not_exists] at this
    rcases this with ‚ü®z, hz‚ü©
    grw [isSurjective, IsExtentional.iInf_mem_toBVSet_himp (by fun_prop), iInf_le _ z,
      IsExtentional.iSup_mem_toBVSet_inf (by fun_prop), inf_iSup_eq]
    apply iSup_le
    intro z'
    grw [iInf_le _ z', ‚Üê inf_top_eq (isFunc _ _ _), ‚Üê ZFSet.toBVSet_mem_toBVSet_of_mem z.2,
      ‚Üê inf_top_eq (isFunc _ _ _), ‚Üê ZFSet.toBVSet_mem_toBVSet_of_mem (g z').2,
      ‚Üê inf_top_eq (isFunc _ _ _), ‚Üê ZFSet.toBVSet_mem_toBVSet_of_mem z'.2, isFunc_unique']
    simp [ZFSet.toBVSet_eq_toBVSet_of_ne (Subtype.val_injective.ne (hz z'))]
  ¬∑ rw [‚Üê lift_lt, ‚Üê ZFSet.cardinalMk_coe_sort, lift_aleph0, aleph0_lt_mk_iff] at hx
    by_contra
    grw [‚Üê ne_eq, ‚Üê bot_lt_iff_ne_bot, ‚Üê inf_top_eq (_ ‚â§·¥Æ _), ‚Üê hxne, cardLE_inf_ne_empty_le_isSurjective, bot_lt_iSup] at this
    rcases this with ‚ü®f, hf‚ü©
    generalize ha : isFunc (y.toBVSet : BVSet B) x.toBVSet f ‚äì isSurjective y.toBVSet x.toBVSet f = a at hf
    rw [‚Üê inf_idem a] at hf
    nth_grw 2 [‚Üê ha, inf_le_right] at hf
    rw [isSurjective, IsExtentional.iInf_mem_toBVSet_himp (by fun_prop), inf_iInf] at hf
    apply forall_lt_of_lt_iInf at hf
    conv at hf =>
      intro z
      rw [IsExtentional.iSup_mem_toBVSet_inf (by fun_prop), inf_iSup_eq, bot_lt_iSup]
    choose g hg using hf
    rcases exists_uncountable_fiber g h hx with ‚ü®z, hz‚ü©
    apply hz.not_countable
    rw [Set.countable_coe_iff]
    apply Set.countable_of_injective_of_countable_image
      (f := fun z' => a ‚äì kpair z.1.toBVSet z'.1.toBVSet ‚àà·¥Æ f)
    ¬∑ rintro z‚ÇÅ hz‚ÇÅ z‚ÇÇ hz‚ÇÇ h
      simp only [Set.mem_preimage, Set.mem_singleton_iff] at hz‚ÇÅ hz‚ÇÇ h
      have := hg z‚ÇÅ
      rw [‚Üê inf_idem (a ‚äì _)] at this
      nth_rw 2 [hz‚ÇÅ] at this
      rw [h, hz‚ÇÅ, ‚Üê inf_inf_distrib_left, ‚Üê inf_assoc, ‚Üê ha] at this
      nth_grw 3 [inf_le_left] at this
      rw [‚Üê inf_top_eq (isFunc _ _ _), ‚Üê ZFSet.toBVSet_mem_toBVSet_of_mem z‚ÇÇ.2,
        ‚Üê inf_top_eq (isFunc _ _ _), ‚Üê ZFSet.toBVSet_mem_toBVSet_of_mem z‚ÇÅ.2,
        ‚Üê inf_top_eq (isFunc _ _ _), ‚Üê ZFSet.toBVSet_mem_toBVSet_of_mem z.2] at this
      grw [isFunc_unique'] at this
      by_contra! hne
      simp [ZFSet.toBVSet_eq_toBVSet_of_ne (Subtype.val_injective.ne hne)] at this
    ¬∑ apply CountableChainCondition.ccc
      apply Set.Pairwise.image
      simp only [Set.Pairwise, Set.mem_preimage, Set.mem_singleton_iff, ne_eq, Function.onFun]
      rintro z‚ÇÅ hz‚ÇÅ z‚ÇÇ hz‚ÇÇ hne
      by_contra
      rw [disjoint_iff, ‚Üê ne_eq, ‚Üê bot_lt_iff_ne_bot, ‚Üê inf_inf_distrib_left, ‚Üê inf_assoc, ‚Üê ha] at this
      nth_grw 3 [inf_le_left] at this
      rw [‚Üê inf_top_eq (isFunc _ _ _), ‚Üê ZFSet.toBVSet_mem_toBVSet_of_mem z‚ÇÇ.2,
        ‚Üê inf_top_eq (isFunc _ _ _), ‚Üê ZFSet.toBVSet_mem_toBVSet_of_mem z‚ÇÅ.2,
        ‚Üê inf_top_eq (isFunc _ _ _), ‚Üê ZFSet.toBVSet_mem_toBVSet_of_mem z.2] at this
      grw [isFunc_unique'] at this
      simp [ZFSet.toBVSet_eq_toBVSet_of_ne (Subtype.val_injective.ne hne)] at this

def isCardinal (u : BVSet B) :=
  isOrdinal u ‚äì ‚®Ö x, x ‚àà·¥Æ u ‚á® (u ‚â§·¥Æ x)·∂ú

@[fun_prop] protected theorem IsExtentional.isCardinal : IsExtentional (B := B) isCardinal := by
  unfold isCardinal
  fun_prop

@[gcongr] theorem isCardinal_congr {u v : BVSet B} (h : u ‚âà v) :
    isCardinal u = isCardinal v := by
  apply IsExtentional.congr _ h
  fun_prop

theorem _root_.ZFSet.isCardinal_toBVSet [Small.{v} B] [CountableChainCondition B] {c : Cardinal} :
    isCardinal c.ord.toZFSet.toBVSet = (‚ä§ : B) := by
  rw [isCardinal, ZFSet.isOrdinal_toBVSet, top_inf_eq, eq_top_iff, IsExtentional.iInf_mem_toBVSet_himp (by fun_prop)]
  refine le_iInf fun ‚ü®_, h‚ü© => ?_
  simp only [Ordinal.mem_toZFSet_iff, gt_iff_lt] at h
  rcases h with ‚ü®o, ho, rfl‚ü©
  rw [lt_ord] at ho
  rw [ZFSet.cardLE_toBVSet_of_card_gt_card, compl_bot]
  simpa

end BVSet
